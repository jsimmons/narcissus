#version 460

#extension GL_GOOGLE_include_directive : require

#extension GL_EXT_buffer_reference : require
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_scalar_block_layout : require
#extension GL_EXT_control_flow_attributes : require

#extension GL_KHR_shader_subgroup_arithmetic : require
#extension GL_KHR_shader_subgroup_ballot : require
#extension GL_KHR_shader_subgroup_vote : require

#include "draw_2d.h"

struct ResolveConstants {
    uint tile_stride;
    uint draw_buffer_len;

    CommandRef draw_buffer;
    ScissorRef scissor_buffer;
    GlyphRef glyph_buffer;
    CoarseReadRef coarse_buffer;
    FineRef fine_buffer;
    TileRef tile_buffer;
};

layout(std430, push_constant) uniform ResolveConstantsBlock {
    ResolveConstants constants;
};

layout (local_size_x_id = 0, local_size_y = 1, local_size_z = 1) in;

void main() {
    const uint local_id = gl_SubgroupID * gl_SubgroupSize + gl_SubgroupInvocationID;
    const uint x = gl_GlobalInvocationID.y;
    const uint y = gl_GlobalInvocationID.z;
    const uint tile_offset = constants.tile_stride * y + x;
    const uint search = ((y & 0xff) << 24) | ((x & 0xff) << 16);
    const uint count = constants.coarse_buffer.values[0];

    if (count == 0) {
        constants.tile_buffer.values[tile_offset].index_min = 0;
        constants.tile_buffer.values[tile_offset].index_max = 0;
        return;
    }

    // Binary search for the upper bound of the tile.
    uint base = 0;
    {
        uint n = count;
        uint mid;
        uint max_iters = 32;
        while (max_iters --> 0 && (mid = n / 2) > 0) {
            const uint value = constants.coarse_buffer.values[1 + base + mid] & 0xffff0000;
            base = value > search ? base : base + mid;
            n -= mid;
        }
    }

    const vec2 tile_min = uvec2(x, y) * TILE_SIZE;
    const vec2 tile_max = tile_min + TILE_SIZE;

    bool hit_opaque = false;
    uint lo = base + 1;
    const uint hi = base + 1;
    for(; !hit_opaque && lo > 0; lo--) {
        const uint i = lo;
        const uint packed = constants.coarse_buffer.values[i];

        if ((packed & 0xffff0000) != (search & 0xffff0000)) {
            break;
        }

        const uint draw_offset = packed & 0xffff;
        const uint draw_index = draw_offset * gl_WorkGroupSize.x + local_id;

        bool intersects = false;
        bool opaque_tile = false;

        if (draw_index < constants.draw_buffer_len) {
            vec2 cmd_min = vec2(99999.9);
            vec2 cmd_max = vec2(-99999.9);

            const uint packed_type = constants.draw_buffer.values[draw_index].packed_type;
            const uint cmd_type = packed_type >> 24;
            const uint cmd_scissor = packed_type & 0xffff;

            const Scissor scissor = constants.scissor_buffer.values[cmd_scissor];

            // If the tile doesn't intersect the scissor region it doesn't need to do work here.
            if (any(lessThan(scissor.offset_max, tile_min)) || any(greaterThan(scissor.offset_min, tile_max))) {
               intersects = false;
            } else {
                for (;;) {
                    const uint scalar_type = subgroupBroadcastFirst(cmd_type);
                    [[branch]]
                    if (scalar_type == cmd_type) {
                        switch (scalar_type) {
                            case DRAW_2D_CMD_RECT:
                                const CmdRect cmd_rect = decode_rect(constants.draw_buffer.values[draw_index]);
                                cmd_min = cmd_rect.position;
                                cmd_max = cmd_rect.position + cmd_rect.bound;

                                const bool background_opaque = (cmd_rect.background_color & 0xff000000) == 0xff000000;
                                if (background_opaque) {
                                    const float border_width = float((packed_type >> 16) & 0xff);
                                    const bool border_opaque = (cmd_rect.border_color & 0xff000000) == 0xff000000;
                                    const vec4 border_radii = unpackUnorm4x8(cmd_rect.border_radii);
                                    const float max_border_radius = max(border_radii.x, max(border_radii.y, max(border_radii.z, border_radii.w))) * 255.0;
                                    const float shrink = ((2.0 - sqrt(2.0)) * max_border_radius) + (border_opaque ? 0.0 : border_width);

                                    const vec2 cmd_shrunk_min = max(scissor.offset_min, cmd_min + shrink);
                                    const vec2 cmd_shrunk_max = min(scissor.offset_max, cmd_max - shrink);
                                    opaque_tile = all(greaterThan(cmd_shrunk_max, cmd_shrunk_min)) && all(greaterThan(tile_min, cmd_shrunk_min)) && all(lessThan(tile_max, cmd_shrunk_max));
                                }
                                break;
                            case DRAW_2D_CMD_GLYPH:
                                const CmdGlyph cmd_glyph = decode_glyph(constants.draw_buffer.values[draw_index]);
                                const Glyph glyph = constants.glyph_buffer.values[cmd_glyph.index];
                                cmd_min = cmd_glyph.position + glyph.offset_min;
                                cmd_max = cmd_glyph.position + glyph.offset_max;
                                break;
                        }
                        break;
                    }
                }

                cmd_min = max(cmd_min, scissor.offset_min);
                cmd_max = min(cmd_max, scissor.offset_max);
                intersects = !(any(lessThan(tile_max, cmd_min)) || any(greaterThan(tile_min, cmd_max)));
            }
        }

        uint intersects_mask = subgroupBallot(intersects).x;

        if (subgroupAny(opaque_tile)) {
            const uvec4 opaque_tile_ballot = subgroupBallot(opaque_tile);
            const uint first_opaque_tile = subgroupBallotFindMSB(opaque_tile_ballot);
            const uint opaque_mask = ~((1 << first_opaque_tile) - 1);
            intersects_mask &= opaque_mask;
            constants.fine_buffer.values[i] = intersects_mask;
            hit_opaque = true;
        } else {
            constants.fine_buffer.values[i] = intersects_mask;
        }
    }

    constants.tile_buffer.values[tile_offset].index_min = lo + 1;
    constants.tile_buffer.values[tile_offset].index_max = hi + 1;
}
